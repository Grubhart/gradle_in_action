ext.cargoGroup= 'org.codehaus.cargo'
ext.cargoVersion= '1.3.1'

// uncomment to retrieve the dependencies to filesystem using copyDependenciesToLocalDir task
/*
repositories {
    mavenCentral()
}
*/
configurations {
    cargo{
        description = "classpath for cargo ant tasks"
        visible= false

    }
}

task deployToLocalTomcat << {
    FileTree cargodeps = configurations.getByName('cargo').asFileTree
    ant.taskdef(resource:'cargo.tasks',classpath:cargodeps.asPath)

    ant.cargo(containerId: 'tomcat7x',action:'run',output:"$buildDir/output.log"){
        configuration{
            deployable(type:'war',file:'todo.war')
        }
        zipUrlInstaller(installUrl: 'http://archive.apache.org/dist/tomcat/tomcat-7/v7.0.32/bin/apache-tomcat-7.0.32.zip')
    }
}

task copyDependenciesToLocalDir(type:Copy){
    from configurations.cargo.asFileTree
    into "${System.properties['user.home']}/libs/cargo"
}

dependencies{
    cargo fileTree(dir: "${System.properties['user.home']}/libs/cargo", include: '*.jar')
}

